plugins {
    id 'kotlin2js' version "1.3.31"
}

group 'org.pongasoft'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

def kotlinVersion = "1.3.31"

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlinVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-test-js:$kotlinVersion"
}

compileKotlin2Js {
    kotlinOptions {
        moduleKind = "umd"
        sourceMap = true
    }
}

task assembleWeb(type: Sync) {
    configurations.compile.each { File file ->
        from(zipTree(file.absolutePath), {
            includeEmptyDirs = false
            include { fileTreeElement ->
                def path = fileTreeElement.path
                path.endsWith(".js") && (path.startsWith("META-INF/resources/") ||
                        !path.startsWith("META-INF/"))
            }
        })
    }
    from compileKotlin2Js.destinationDir
    into "${buildDir}/web/dynamic/js"

    dependsOn classes
}

processResources {
    destinationDir file("${buildDir}/web/")
}

assemble.dependsOn assembleWeb

